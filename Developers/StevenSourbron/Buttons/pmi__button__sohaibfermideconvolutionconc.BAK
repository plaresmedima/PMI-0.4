;
;
;    Copyright (C) 2009 Steven Sourbron
;
;    This program is free software; you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation; either version 2 of the License, or
;    (at your option) any later version.
;
;    This program is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.
;
;    You should have received a copy of the GNU General Public License along
;    with this program; if not, write to the Free Software Foundation, Inc.,
;    51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA
;
;
;

FUNCTION PMI__Button__Input__sohaibfermideconvolutionconc, top, series, aif, roi, in

    PMI__Info, top, Stdy=Stdy
    DynSeries = Stdy->Names(0,DefDim=3,ind=ind,sel=sel)
	in = {ser:sel, aif:stdy->sel(1), roi:0L, nb:1, T1b:1725.0, T1t:1106.0}

	WHILE 1 DO BEGIN

		in = PMI__Form(top, Title='Perfusion analysis setup', [$
		ptr_new({Type:'DROPLIST',Tag:'ser', Label:'Dynamic series', Value:DynSeries, Select:in.ser}), $
		ptr_new({Type:'DROPLIST',Tag:'aif', Label:'Arterial Region', Value:Stdy->names(1), Select:in.aif}), $
		ptr_new({Type:'DROPLIST',Tag:'roi', Label:'Window', Value:['<entire image>',Stdy->names(1)], Select:in.roi}), $
		ptr_new({Type:'VALUE'	,Tag:'nb' , Label:'Length of baseline (# of dynamics)', Value:in.nb}), $
		ptr_new({Type:'VALUE'	,Tag:'T1b' , Label:'Blood T1 (msec)', Value:in.T1b}), $
		ptr_new({Type:'VALUE'	,Tag:'T1t' , Label:'Tissue T1 (msec)', Value:in.T1t})])
		IF in.cancel THEN return, 0

    	Series = Stdy->Obj(0,ind[in.ser])
    	IF (in.nb LT 1) or (in.nb GT Series->d(3)) THEN BEGIN
    		in.nb = 10
    		msg = ['Baseline length must be less than the total number of dynamics',$
    		'Please select another baseline length']
    	ENDIF ELSE BEGIN
	   		Aif = PMI__RoiCurve(Stdy->DataPath(), Series, Stdy->Obj(1,in.aif), status, cnt=cnt)
    		IF cnt EQ 0 THEN $
    			msg = ['Arterial region is empty on this series',$
    			'Please select another region and/or series'] $
    		ELSE IF n_elements(Aif) NE Series->d(3) THEN $
    			msg = ['Arterial region is not defined on every dynamic',$
    			'Please select another region and/or series'] $
    		ELSE BEGIN
    			if in.roi gt 0 then Roi = Stdy->Obj(1,in.roi-1)
				return, 1
    		ENDELSE
    	ENDELSE
     	IF 'Cancel' EQ dialog_message(msg,/information,/cancel) THEN return, 0
  	ENDWHILE
END



pro PMI__Button__Event__sohaibfermideconvolutionconc, ev

	PMI__Info, ev.top, Status=Status, Stdy=Stdy
	PMI__Message, status, 'Preparing calculation..'

    IF NOT PMI__Button__Input__sohaibfermideconvolutionconc(ev.top,series,aif,roi,in) THEN RETURN

	PMI__Message, status, 'Calculating..'

	Dom = {z:Series->z(), t:Series->t(0), m:Series->m()}
    Sbf = Stdy->New('SERIES', Domain= Dom,  Name= 'Fermi-Gd: Blood Flow (ml/min/g)' )

	d = Series->d()
	time = Series->t() - Series->t(0)

	Hct = 0.55
	relaxivity = 3.8 ;Hz/mM
	TD = 100.0 ;msec
	TR = 2.0 ;msec
	FA = 11.0 ;deg
	Nphase = 48.

	Aif0 = total(Aif[0:in.nB-1])/in.nB
	Aif = Concentration_SR_SpGRE(Aif, Aif0, in.T1b, relaxivity, TD, FA, TR, Nphase)/Hct

	for j=0L,d[2]-1 do begin

		PMI__Message, status, 'Calculating ', j/(d[2]-1E)

		if obj_valid(Roi) then $
			P = PMI__PixelCurve(Stdy->DataPath(),Series,Roi,z=Series->z(j),cnt=cnt,ind=k) $
		else begin
			cnt = d[0]*d[1]
			P = Series->Read(Stdy->DataPath(),z=Series->z(j))
			P = reform(P,cnt,d[3],/overwrite)
			k = lindgen(d[0]*d[1])
		endelse

 		if cnt gt 0 then begin

			if in.nB eq 1 then P0 = reform(P[*,0]) else P0 = total(P[*,0:in.nB-1],2)/in.nB
			nozero = where(P0 NE 0, cnt)

			if cnt gt 0 then begin

				P = P[nozero,*]
    			P0 = P0[nozero]

    			BF = fltarr(d[0],d[1])

				for r=0L,cnt-1 do begin
   					curve = Concentration_SR_SpGRE(reform(P[r,*]), P0[r], in.T1t, relaxivity, TD, FA, TR, Nphase)
   					Par = [0.015, 1.0, 0.5] ;[FP, a, b]
					Fit = FitSingleInlet('Fermi',time, aif, curve, Par, /noderivative, /positivity)
					BF[k[nozero[r]]] = 60D*Par[0]/0.55

				endfor

				Sbf -> Write, Stdy->DataPath(), BF, j

			endif
		endif
	endfor

	Sbf -> Trim, 8, 1

	ColourTable, 1, Red=R, Green=G, Blue=B
	Sbf -> Clr, R, G, B

    PMI__Control, ev.top, /refresh
end


pro PMI__Button__Control__sohaibfermideconvolutionconc, id, v

	PMI__Info, tlb(id), Stdy=Stdy
	if obj_valid(Stdy) then begin
		Series = Stdy->Names(0,ns,DefDim=3)
		Regions = Stdy->Names(1,nr)
		sensitive = (ns gt 0) and (nr gt 0)
	endif else sensitive=0
    widget_control, id, sensitive=sensitive
end

function PMI__Button__sohaibfermideconvolutionconc, parent,value=value,separator=separator

	SingleInletFermi

    if n_elements(value) eq 0 then value = 'Fermi analysis (Pixel)'

    id = widget_button(parent $
    ,   value = value  $
    ,  	event_pro = 'PMI__Button__Event__sohaibfermideconvolutionconc' $
    ,	pro_set_value = 'PMI__Button__Control__sohaibfermideconvolutionconc' $
    ,  	separator = separator )

    return, id
end

